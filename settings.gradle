plugins {
    id 'com.gradle.enterprise' version '3.16.2'
    id 'com.gradle.common-custom-user-data-gradle-plugin' version '1.13'
}

gradleEnterprise {
    server = 'https://ge.grails.org'
    buildScan {
        publishAlwaysIf(System.getenv('CI') == 'true')
        publishIfAuthenticated()
        uploadInBackground = System.getenv('CI') == null
        capture {
            taskInputFiles = true
        }
    }
}

buildCache {
    local { enabled = System.getenv('CI') != 'true' }
    remote(gradleEnterprise.buildCache) {
        def isAuthenticated = System.getenv('GRADLE_ENTERPRISE_ACCESS_KEY')
        push = System.getenv('CI') == 'true' && isAuthenticated
        enabled = true
    }
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            def groovyVersion = System.getenv('GROOVY_VERSION')
            if (groovyVersion) {
                logger.warn "Using custom version $groovyVersion of Groovy due to GROOVY_VERSION being set."
                version('groovy', groovyVersion)
            }
        }
    }
}

rootProject.name = 'grails-spring-security-rest'

include 'spring-security-rest'
include 'spring-security-rest-memcached'
include 'spring-security-rest-redis'
include 'spring-security-rest-grailscache'
include 'spring-security-rest-gorm'
include 'spring-security-rest-testapp-profile'
include 'spring-security-rest-docs'

