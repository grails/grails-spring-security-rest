buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:${project.grailsVersion}"
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}  

dependencies {
    runtime "org.grails.profiles:web:3.1.9"
}

task installKeys << {
    def dir = file("${System.getProperty("user.home")}/.grails/spring-security-rest")
    dir.mkdir()

    copy {
        from "keys"
        into dir
    }
}

build.dependsOn 'installKeys'

task generateProfileConfig << {
    copy {
        from 'profile.yml.tmpl'
        into '.'
        rename { String fileName -> fileName.replaceAll '\\.tmpl', '' }
        expand pluginVersion: project.version
    }

    file('features').eachDir { feature ->
        copy {
            from "features/${feature.name}/feature.yml.tmpl"
            into "features/${feature.name}/"
            rename { String fileName -> fileName.replaceAll '\\.tmpl', '' }
            expand pluginVersion: project.version
        }
    }
}

compileProfile.dependsOn generateProfileConfig