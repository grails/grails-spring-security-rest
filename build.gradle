buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.18.0"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        //classpath "io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE"
        classpath 'com.adarshr:gradle-test-logger-plugin:2.0.0'
    }
}

allprojects {
    apply plugin:"idea"
}

def pluginProjects = ['spring-security-rest', 'spring-security-rest-memcached', 'spring-security-rest-redis', 'spring-security-rest-grailscache', 'spring-security-rest-gorm']
def profileProjects = ['spring-security-rest-testapp-profile']
def publishedProjects = pluginProjects + profileProjects

subprojects { Project project ->
    group "org.grails.plugins"
    version "3.0.1"

    ext {
        grailsVersion = project.grailsVersion
        gradleWrapperVersion = project.gradleWrapperVersion
        isSnapshot = version.endsWith('SNAPSHOT')
        bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ''
        bintrayKey = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : ''
        pluginPortalUser = project.hasProperty('pluginPortalUser') ? project.property('pluginPortalUser') : ''
        pluginPortalPassword = project.hasProperty('pluginPortalPassword') ? project.property('pluginPortalPassword') : ''
    }

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    if (project.name != "spring-security-rest-docs" &&
        project.name != "spring-security-rest-testapp-profile" &&
            !project.name.startsWith("build") ) {
        apply plugin: "org.grails.grails-plugin"
        apply plugin: "org.grails.grails-plugin-publish"
        apply plugin: "com.jfrog.artifactory"
    }

    if (project.name in pluginProjects) {

        sourceCompatibility = targetCompatibility = 1.8

        dependencies {
            console "org.grails:grails-console"

            compile "org.springframework.boot:spring-boot-starter-logging"
            compile "org.springframework.boot:spring-boot-autoconfigure"
            compile "org.grails:grails-core"

            provided "org.grails:grails-plugin-services"
            provided "org.grails:grails-plugin-domain-class"

            testCompile "org.grails:grails-gorm-testing-support"
            //testCompile "org.grails:grails-plugin-testing"
            testCompile "org.grails:grails-web-testing-support"
            testCompile('com.athaydes:spock-reports:1.7.1') {
                transitive = false
            }
        }

        apply plugin: 'com.adarshr.test-logger'

        testlogger {
            showFullStackTraces true
            showStandardStreams true
            showPassedStandardStreams false
            showSkippedStandardStreams false
            showFailedStandardStreams true
        }

        artifactoryPublish {
            dependsOn sourcesJar, javadocJar
        }
    }

    if (project.name in profileProjects) {
        apply plugin: "org.grails.grails-profile"
        apply plugin: "org.grails.grails-profile-publish"
    }

    if (project.name in publishedProjects && project.name != "spring-security-rest-docs" &&
            project.name != "spring-security-rest-testapp-profile") {
        grailsPublish {
            user = bintrayUser
            key = bintrayKey
            portalUser = pluginPortalUser
            portalPassword = pluginPortalPassword
            userOrg = 'grails'
            repo = 'plugins'
            githubSlug = 'alvarosanchez/grails-spring-security-rest'
            license {
                name = 'Apache-2.0'
            }
            title = "Spring Security REST plugin"
            desc = "Grails plugin to implement token-based, RESTful authentication using Spring Security"
            developers = [
                alvarosanchez: "Alvaro Sanchez-Mariscal",
                Schlogen: "James Kleeh"
            ]
        }

        artifactory {
            contextUrl = 'http://oss.jfrog.org'
            publish {
                repository {
                    repoKey = isSnapshot ? 'oss-snapshot-local' : 'oss-release-local'
                    username = bintrayUser
                    password = bintrayKey
                }
                defaults {
                    // Reference to Gradle configurations defined in the build script.
                    // This is how we tell the Artifactory Plugin which artifacts should be
                    // published to Artifactory.
                    publications('maven')
                }
            }
        }
    }
}

task aggregateGroovyDoc(type: Groovydoc) {
    def groovyDocProjects = subprojects.findAll { it.name in pluginProjects }
    group = JavaBasePlugin.DOCUMENTATION_GROUP

    dependsOn groovyDocProjects.groovydoc
    source groovyDocProjects.groovydoc.source
    destinationDir file("${buildDir}/docs/groovydoc")
    classpath = files(groovyDocProjects.groovydoc.classpath)
    groovyClasspath = files(groovyDocProjects.groovydoc.groovyClasspath)
}