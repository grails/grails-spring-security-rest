apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
	isReleaseVersion = !version.toString().endsWith("SNAPSHOT")
}

publishing {
	publications {
		maven(MavenPublication) {
			artifactId = project.name
			groupId = project.group
			version = project.version

			from components.java

			artifact sourcesJar
			artifact javadocJar

			pom {
				name = 'Spring Security REST plugin'
				description = 'Grails plugin to implement token-based, RESTful authentication using Spring Security'
				url = project.projectUrl
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'alvarosanchez'
						name = 'Alvaro Sanchez-Mariscal'
						email = ''
					}
					developer {
						id = 'jameskleeh'
						name = 'James Kleeh'
						email = ''
					}
					developer {
						id = 'jdaugherty'
						name = "James Daugherty"
						email = ''
					}
				}
				scm {
					connection = project.scmConnection
					developerConnection = project.scmDeveloperConnection
					url = project.scmUrl
				}
			}
		}
	}
}

afterEvaluate {
	signing {
		ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
		ext["signing.password"] = System.getenv('SIGNING_PASSPHRASE')
		ext["signing.secretKeyRingFile"] = System.getenv('SECRING_FILE')

		required {
			isReleaseVersion && gradle.taskGraph.hasTask("publish")
		}
		sign publishing.publications.maven
	}
}

tasks.withType(Sign) {
	onlyIf { isReleaseVersion }
}
